package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;
import rs.ac.bg.etf.pp1.ast.*;
import rs.ac.bg.etf.pp1.ast.*;


parser code {:

    boolean errorDetected;
	Logger log = Logger.getLogger(getClass());


    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke

    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }

    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
    }

    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
        errorDetected = true;
    	StringBuilder msg = new StringBuilder(message);
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.error(msg.toString());
    }

:}

init with {:
	errorDetected = false;
:}

scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null)
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}


/* declaracija terminala i neterminala */


terminal PROGRAM, LC_BRACE, RC_BRACE, SEMI, COMMA, CONST, L_BRACE, R_BRACE, VOID;
terminal R_PAREN, L_PAREN, IF, ELSE, READ, PRINT, FOR, BREAK, CONTINUE, RETURN, INC, DEC, EQUAL, DOT;
terminal EQUALS, NOT_EQUALS, LESS, LESS_EQUALS, GREATER, GREATER_EQUALS, MINUS, PLUS, DIV, MUL, MOD, NEW, OR, AND;
terminal MOD_ASSIGN, MUL_ASSIGN, DIV_ASSIGN, PLUS_ASSIGN, MINUS_ASSIGN, FOREACH, COLON;
terminal String IDENT;
terminal Integer NUMBER;
terminal Character CHAR;
terminal Integer  BOOL;

non terminal DeclarationList, MethodDeclarationList, VarDeclarationList, VarDeclaration;
non terminal ConstDeclarationList, ConstDeclaration, ConstDeclarationActual, Var;
non terminal MethodDeclaration, MethodDeclarationActual, FormalParams, StatementList;
non terminal FormalParamList, Statement, DesignatorStatement;
non terminal DesignatorStatementList, ElseStatement;
non terminal AddOpRight, AddOpLeft, MulOpLeft, MulOpRight, AssignOps, AssignOp;
non terminal ConditionOptional, ActualParams;
non terminal Declaration;

nonterminal rs.etf.pp1.symboltable.concepts.Obj ProgName, Program, VarType, MethodTypeName, MethodVoidName, Designator, RelOp, LeftSideAssign;

nonterminal rs.etf.pp1.symboltable.concepts.Struct Type, Const, MethodCall, Factor, Expression, Term, TermDef, Condition, ConditionFact, ConditionTerm;

precedence left ELSE;

Program ::= (Program)  PROGRAM ProgName DeclarationList LC_BRACE MethodDeclarationList RC_BRACE;

ProgName ::= (ProgName) IDENT:progName;

DeclarationList ::=    (DeclarationListDeclaration) DeclarationList Declaration
                   |   (NoDeclarationList) /* epsilon */;

Declaration ::=   (DeclarationListVar) VarDeclaration
              |   (DeclarationListConst) ConstDeclaration;

VarDeclaration ::=  (VarDeclarationV) VarType VarDeclarationList  SEMI
                 |  (VarDeclarationError) error:e SEMI
                          {:
                              parser.report_error("Uspesan oporavak od greske na liniji " + eleft +
                                     " pri deklarisanju varijable, do ;", null);
                          :};

VarType ::= (VarType) Type:type;

VarDeclarationList ::=     (VarDeclarationListVar) VarDeclarationList COMMA Var
                      |    (VarDecl) Var
                      |    (VarDeclError) error:e
                            {:
                                parser.report_error("Uspesan oporavak od greske na liniji " + eleft +
                                     " pri deklarisanju varijable do ,", null);
                            :};

Var ::=    (Ident) IDENT:name
       |   (IdentArray) IDENT:name L_BRACE R_BRACE;

Type ::=  (Type) IDENT:typeName;

ConstDeclaration ::=     (ConstDeclarationTypeListSemi) CONST VarType ConstDeclarationList SEMI
                    |    (ConstDeclarationError) CONST error:e SEMI
                          {:
                              parser.report_error("Uspesan oporavak od greske na liniji " + eleft +
                                   " pri definisanju konstante do ;", null);
                          :};

ConstDeclarationList ::=      (ConstDeclarationListComaActual) ConstDeclarationList COMMA ConstDeclarationActual
                        |     (ConstDecl) ConstDeclarationActual
                        |     (ConstDeclError) error:e
                                        {:
                                           parser.report_error("Uspesan oporavak od greske na liniji " + eleft +
                                                       " pri definisanju konstante do ,", null);
                                        :};


ConstDeclarationActual ::=     (ConstDeclarationActual) IDENT:name AssignOp Const;

Const ::=   (NumberConst) NUMBER:val
         |  (BoolConst) BOOL:val
         |  (CharConst) CHAR:val;


MethodDeclarationList ::=    (MethodDeclarationListMethod) MethodDeclarationList MethodDeclaration
                        |    (NoMethodDeclarationList) /* epsilon */;

MethodDeclaration ::=    (TypeMethodDeclaration) MethodTypeName MethodDeclarationActual
                    |    (VoidMethodDeclaration) MethodVoidName MethodDeclarationActual;

MethodTypeName ::= (MethodTypeName) Type:retType IDENT:methodName;

MethodVoidName ::= (MethodVoidName) VOID IDENT:methodName;

MethodDeclarationActual ::=  (MethodDeclarationActual) L_PAREN FormalParams R_PAREN
                             DeclarationList LC_BRACE StatementList RC_BRACE;

FormalParams ::=         (FormalParamsList) FormalParamList:n
                 |       (NoFormalParamsList) /* epsilon */
                 |       (FormalParamErrorComma)  error:e COMMA FormalParamList:n
                           {:
                                parser.report_error("Uspesan oporavak od greske na liniji " + eleft +
                                          " u formalnim parametrima do ,", null);
                           :}
                 |       (FormalParamErrorParen) error:e
                         {:
                            parser.report_error("Uspesan oporavak od greske na liniji " + eleft +
                                      " u formalnim parametrima do )", null);
                          :};

FormalParamList ::=     (FormalParamListActual) FormalParamList COMMA Type Var
                   |    (FormalParamVar) Type Var;

StatementList ::=     (StatementListStatement) StatementList Statement
                 |    (NoStatementList) /* epsilon */;


Statement ::=           (IfStatement) IF L_PAREN Condition R_PAREN Statement ElseStatement
                    |   (DesignatorStatementStatement) DesignatorStatement SEMI
                    |   (ReadStatement) READ L_PAREN Designator R_PAREN SEMI
                    |   (PrintExpressionStatement) PRINT L_PAREN Expression R_PAREN SEMI
                    |   (PrintExpressionNumStatement) PRINT L_PAREN Expression COMMA NUMBER R_PAREN SEMI
                    |   (ForStatement) FOR L_PAREN DesignatorStatementList SEMI ConditionOptional SEMI
                                       DesignatorStatementList R_PAREN Statement
                    |   (ForeachStatement) FOREACH L_PAREN IDENT COLON Designator R_PAREN Statement
                    |   (BrakStatement) BREAK SEMI
                    |   (ContinueStatement) CONTINUE SEMI
                    |   (ReturnStatementEmpty) RETURN SEMI
                    |   (ReturnStatementExpression) RETURN Expression SEMI
                    |   (CurlyBraceStatement) LC_BRACE StatementList RC_BRACE
                    |   (ErrorStatement) error:e SEMI
                                        {:
                                           parser.report_error("Uspesan oporavak od greske na liniji "
                                                        + eleft + " u statement, do ; ", null);
                                        :};

ElseStatement ::= (ElseStatementStatement) ELSE Statement
                | (NoElseStatement) /* epsilon */;


DesignatorStatement ::=     (LeftDesignatorAssignExpression) LeftSideAssign:dest AssignOps Expression:e
                       |    (DesignatorStatementIncrement) Designator INC
                       |    (DesignatorStatementDecrement) Designator DEC
                       |    (DesignatorStatementMethodCall) MethodCall;

LeftSideAssign::= (LeftSideAssignAssignOps) LeftSideAssign:dest AssignOps Designator:d
                | (LeftSideAssignVar) Designator:d;


Designator ::=      (DesignatorArray) IDENT:name L_BRACE Expression R_BRACE
            |       (DesignatorOnly) IDENT:name;

DesignatorStatementList ::= (DesignatorStatementOnly) DesignatorStatement
                           | (NoDesignatorStatementList) /* epsilon */;

MethodCall ::=    (MethodCallActualParams) Designator L_PAREN ActualParams R_PAREN
              |   (MethodCallNoParams) Designator L_PAREN R_PAREN;

ActualParams ::=   (ActualParamsList) ActualParams COMMA Expression
               |   (ActualParamsExpression) Expression;


ConditionOptional ::= (ConditionOptionalCondition) Condition
                    | (ForStatementError) error:e
                                {:
                                    parser.report_error("Uspesan oporavak od greske na liniji "
                                    + eleft + " logicki iskaz u for petlji, do ; ", null);
                                :}
                    | (NoConditionOptional) /* epsilon */;

Condition ::=      (ConditionListTerm) Condition:te OR ConditionTerm:t
            |      (ConditionLastTerm) ConditionTerm:t;

ConditionTerm ::=    (ConditionListFact) ConditionTerm:te AND ConditionFact:t
                |    (ConditionLastFact) ConditionFact:t;


ConditionFact ::=     (ConditionFactList) Expression:te RelOp Expression:t
                |     (ConditionFactExpression) Expression:t;


Expression ::=    (ExpressionAddopLeftList) Expression:te AddOpLeft Term:t
              |   (ExpressionTermDef) TermDef:t;

TermDef ::=   (MinusTerm) MINUS Term:t
            | (OnlyTerm) Term:t;

Term ::=    (TermMullOpLeftList) Term:te MulOpLeft Factor:t
       |    (TermFactor) Factor:t;

Factor ::=         (FactorConst) Const:t
          |        (FactorMethodCall) MethodCall:t
          |        (FactorDesignator) Designator:t
          |        (FactorParenExoression) L_PAREN Expression:t R_PAREN
          |        (FactorNewTypeExpression) NEW Type L_BRACE Expression R_BRACE;


RelOp ::= (RelOpEquals) EQUALS:rel
        | (RelOpNotEquals) NOT_EQUALS:rel
        | (RelOpLess) LESS:rel
	    | (RelOpLessEqual) LESS_EQUALS:rel
	    | (RelOpGreater) GREATER:rel
	    | (RelOpGreaterEquals) GREATER_EQUALS:rel;

AddOpLeft ::= (AddOpLeftPlus) PLUS | (AddOpLeftMinus) MINUS;

AddOpRight ::= (AddOpRightPlus) PLUS_ASSIGN | (AddOpRightMinus) MINUS_ASSIGN;

MulOpLeft ::= (MulOpLeftMul) MUL | (MulOpLeftDiv) DIV | (MulOpLeftMod) MOD;

MulOpRight ::= (MulOpRightMul) MUL_ASSIGN | (MulOpRightDiv) DIV_ASSIGN | (MulOpRightMod) MOD_ASSIGN;

AssignOp ::= (AssignOp) EQUAL;

AssignOps ::= (AssignOpEqual) AssignOp | (AssignOpAdd) AddOpRight | (AssignOpMul) MulOpRight;








